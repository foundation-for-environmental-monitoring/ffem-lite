plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs'
    id 'com.google.firebase.crashlytics'
}

def secretsFile = file(System.properties['user.home'] + '/.gradle/gradle.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "io.ffem.lite"
        minSdkVersion 23
        //noinspection OldTargetApi
        targetSdkVersion 29
        versionCode 61
        versionName "Alpha 0.44"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = "1.8" }

    signingConfigs {
        release {
            if (secrets.getProperty('FFEM_RELEASE_STORE_FILE')) {
                storeFile file(secrets.getProperty('FFEM_RELEASE_STORE_FILE'))
                storePassword secrets.getProperty('FFEM_RELEASE_STORE_PASSWORD')
                keyAlias secrets.getProperty('FFEM_RELEASE_KEY_ALIAS')
                keyPassword secrets.getProperty('FFEM_RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            if (secrets.getProperty('FFEM_RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled(true)
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" +
                    (System.currentTimeMillis()) + "L)"
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "ffem-lite-${variant.versionCode}.apk"
                }
            }
        }
    }

    defaultConfig {
        buildConfigField "java.util.concurrent.atomic.AtomicBoolean", "INSTRUMENTED_TEST_RUNNING",
                "new java.util.concurrent.atomic.AtomicBoolean(false)"
        buildConfigField "Boolean", "TAKE_SCREENSHOTS", "false"
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + 0L + ")"
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            events "started", "skipped", "passed", "failed"
            showStandardStreams true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.preference:preference:1.1.1'

    implementation 'com.google.android.play:core:1.8.0'

    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'

    implementation 'com.google.android.gms:play-services-vision:20.1.2'

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Firebase
    implementation 'com.google.firebase:firebase-ml-vision:24.1.0'
    implementation 'com.google.firebase:firebase-ml-vision-barcode-model:16.1.2'
    implementation 'com.google.firebase:firebase-analytics-ktx:17.5.0'
    releaseImplementation 'com.google.firebase:firebase-crashlytics:17.2.1'

    // Navigation library
    def nav_version = "2.3.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // CameraX
    def camerax_version = "1.0.0-beta10"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:1.0.0-alpha17"

    // Dagger
    kapt 'com.google.dagger:dagger-compiler:2.28.3'
    kapt 'com.google.dagger:dagger-android-processor:2.28.3'
    implementation 'com.google.dagger:dagger-android:2.28.3'
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.28.3'

    // Logs
    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0') {
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.3.1'

    // UIAutomator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

}
apply plugin: 'com.google.gms.google-services'